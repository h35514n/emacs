;;; dm-evil-text --- Summary: Daymacs Evil text objects and sorting  -*- lexical-binding: t; -*-

;;; Commentary:

;;; Code:

(require 'cl-lib)
(require 'evil)

(declare-function copy-as-format "copy-as-format")
(defvar evil-commentary-mode-map)

;; ----------------------
;; Word char adjustments
;; ----------------------

(defun dm-evil-text--add-dot-to-word-chars-h ()
  "Add dot to the word chars syntax entry list."
  (modify-syntax-entry ?. "w"))

(defun dm-evil-text--add-underscore-to-word-chars-h ()
  "Add underscore to the word chars syntax entry list."
  (modify-syntax-entry ?_ "w"))

(defun dm-evil-text--add-dash-to-word-chars-h ()
  "Add dash to the word chars syntax entry list."
  (modify-syntax-entry ?- "w"))

(defun dm-evil-text--add-to-word-char-list ()
  "Customize the word char list in prog and other modes."
  (add-hook 'LaTeX-mode-hook #'dm-evil-text--add-dot-to-word-chars-h)
  (add-hook 'emacs-lisp-mode-hook #'dm-evil-text--add-dash-to-word-chars-h)
  (add-hook 'latex-mode-hook #'dm-evil-text--add-dot-to-word-chars-h)
  (add-hook 'markdown-mode-hook #'dm-evil-text--add-underscore-to-word-chars-h)
  (add-hook 'org-mode-hook #'dm-evil-text--add-underscore-to-word-chars-h)
  (add-hook 'prog-mode-hook #'dm-evil-text--add-dash-to-word-chars-h)
  (add-hook 'prog-mode-hook #'dm-evil-text--add-underscore-to-word-chars-h)
  (add-hook 'python-mode-hook #'dm-evil-text--add-underscore-to-word-chars-h)
  (add-hook 'text-mode-hook #'dm-evil-text--add-underscore-to-word-chars-h)
  nil)

;; ------------
;; Text objects
;; ------------

(defmacro dm-evil-text--define-and-bind-text-object (name key start-regex end-regex)
  "Define inner and outer Evil text objects named NAME on KEY.
START-REGEX and END-REGEX are passed to `evil-select-paren'."
  (let ((inner-name (intern (concat "evil-inner-" name)))
        (outer-name (intern (concat "evil-a-" name))))
    `(progn
       (evil-define-text-object ,inner-name (count &optional beg end type)
         (evil-select-paren ,start-regex ,end-regex beg end type count nil))
       (evil-define-text-object ,outer-name (count &optional beg end type)
         (evil-select-paren ,start-regex ,end-regex beg end type count t))
       (define-key evil-inner-text-objects-map ,key #',inner-name)
       (define-key evil-outer-text-objects-map ,key #',outer-name))))

;; ------------
;; Sort motion
;; ------------

(defun dm-evil-text-sort-inner (textobj &optional desc)
  "Sort inside the TEXTOBJ surrounding point.
When DESC is non-nil, sort in descending order.  TEXTOBJ should
name the suffix of an `evil-inner-*' text object."
  (let ((evil-textobj (intern (format "evil-inner-%s" textobj)))
        (start-pos (point)))
    (save-excursion
      (let* ((bounds (call-interactively evil-textobj))
             (beg (cl-first bounds))
             (end (cl-second bounds)))
        (sort-lines desc beg end)))
    (goto-char start-pos)))

(defun dm-evil-text-sort-inner-paragraph (desc)
  "Sort inside the paragraph under point.
With prefix argument DESC, sort in descending order."
  (interactive "P")
  (dm-evil-text-sort-inner 'paragraph desc))

(defun dm-evil-text-sort-inner-buffer (desc)
  "Sort inside the current buffer.
With prefix argument DESC, sort in descending order."
  (interactive "P")
  (dm-evil-text-sort-inner 'buffer desc))

(defun dm-evil-text-sort-inner-curly (desc)
  "Sort inside the current curly braces.
With prefix argument DESC, sort in descending order."
  (interactive "P")
  (dm-evil-text-sort-inner 'curly desc))

(defun dm-evil-text-sort-inner-paren (desc)
  "Sort inside the current parentheses.
With prefix argument DESC, sort in descending order."
  (interactive "P")
  (dm-evil-text-sort-inner 'paren desc))

(defun dm-evil-text-sort-inner-bracket (desc)
  "Sort inside the current brackets.
With prefix argument DESC, sort in descending order."
  (interactive "P")
  (dm-evil-text-sort-inner 'bracket desc))

;; ----------------------------------
;; Copy-as-format (vim: yank) motions
;; ----------------------------------

(defun dm-evil-text-copy-inner (textobj)
  "Copy the TEXTOBJ surrounding point.
When DESC is non-nil, copy in descending order.  TEXTOBJ should
name the suffix of an `evil-inner-*' text object."
  (let ((evil-textobj (intern (format "evil-inner-%s" textobj)))
        (start-pos (point)))
    (save-excursion
      (let* ((bounds (call-interactively evil-textobj))
             (beg (cl-first bounds))
             (end (cl-second bounds)))
        (goto-char beg)
        (set-mark end)
        (activate-mark)
        (setq current-prefix-arg '(4))
        (copy-as-format)))
    (goto-char start-pos)))

(defun dm-evil-text-copy-inner-paragraph (desc)
  "Copy inside the paragraph under point.
With prefix argument DESC, copy in descending order."
  (interactive "P")
  (dm-evil-text-copy-inner 'paragraph))

(defun dm-evil-text-copy-inner-buffer (desc)
  "Copy inside the current buffer.
With prefix argument DESC, copy in descending order."
  (interactive "P")
  (dm-evil-text-copy-inner 'buffer))

(defun dm-evil-text-copy-inner-curly (desc)
  "Copy inside the current curly braces.
With prefix argument DESC, copy in descending order."
  (interactive "P")
  (dm-evil-text-copy-inner 'curly))

(defun dm-evil-text-copy-inner-paren (desc)
  "Copy inside the current parentheses.
With prefix argument DESC, copy in descending order."
  (interactive "P")
  (dm-evil-text-copy-inner 'paren))

(defun dm-evil-text-copy-inner-bracket (desc)
  "Copy inside the current brackets.
With prefix argument DESC, copy in descending order."
  (interactive "P")
  (dm-evil-text-copy-inner 'bracket))

;; ---------------
;; Change behavior
;; ---------------

(defun dm-evil-text-change-back-to-indentation ()
  "Delete current line contents back to indentation and enter Evil insert state.

This preserves leading indentation and removes everything from the first
non-whitespace character through the current position."
  (interactive)
  (let ((indent-pos (save-excursion
                      (back-to-indentation)
                      (point)))
        (curr-pos (point)))
    (kill-region indent-pos curr-pos)
    (goto-char indent-pos)
    (evil-insert-state)))

(defun dm-evil-text-change-to-end-of-line ()
  "Delete from the current position to the end of the current line.
Then enter Evil insert state."
  (interactive)
  (kill-line)
  (evil-insert-state))

;; ---------------
;; Setup
;; ---------------

;;;###autoload
(defun dm-evil-text-setup ()
  "Install Daymacs Evil text objects and sort bindings."
  ;; text objects
  (dm-evil-text--define-and-bind-text-object "bracket" "[" "\\[" "\\]")
  (dm-evil-text--define-and-bind-text-object "dash" "-" "-" "-")
  (dm-evil-text--define-and-bind-text-object "dollar" "$" "\\$" "\\$")
  (dm-evil-text--define-and-bind-text-object "pipe" "|" "|" "|")
  (dm-evil-text--define-and-bind-text-object "slash" "/" "/" "/")
  (dm-evil-text--define-and-bind-text-object "underscore" "_" "_" "_")
  (evil-define-text-object evil-inner-buffer (count &optional beg end type)
    "Select inner buffer."
    :type line
    (evil-select-inner-object 'buffer beg end type count t))

  ;; sort motions
  (evil-define-key* 'normal 'global
    (kbd "g s i p") #'dm-evil-text-sort-inner-paragraph
    (kbd "g s i g") #'dm-evil-text-sort-inner-buffer
    (kbd "g s i {") #'dm-evil-text-sort-inner-curly
    (kbd "g s i }") #'dm-evil-text-sort-inner-curly
    (kbd "g s i [") #'dm-evil-text-sort-inner-bracket
    (kbd "g s i ]") #'dm-evil-text-sort-inner-bracket
    (kbd "g s i (") #'dm-evil-text-sort-inner-paren
    (kbd "g s i )") #'dm-evil-text-sort-inner-paren)

  ;; yank motions
  (evil-define-key* 'normal 'global
    (kbd "g y i p") #'dm-evil-text-copy-inner-paragraph
    (kbd "g y i g") #'dm-evil-text-copy-inner-buffer
    (kbd "g y i {") #'dm-evil-text-copy-inner-curly
    (kbd "g y i }") #'dm-evil-text-copy-inner-curly
    (kbd "g y i [") #'dm-evil-text-copy-inner-bracket
    (kbd "g y i ]") #'dm-evil-text-copy-inner-bracket
    (kbd "g y i (") #'dm-evil-text-copy-inner-paren
    (kbd "g y i )") #'dm-evil-text-copy-inner-paren)
  (with-eval-after-load 'evil-commentary
    ;; Let the global `g y i ...' bindings win over commentary's `gy' operator.
    (evil-define-key* 'normal evil-commentary-mode-map
      (kbd "g y") nil))

  ;; word chars
  (dm-evil-text--add-to-word-char-list)

  ;; line changes
  (evil-define-key* 'normal 'global
    (kbd "S")   #'dm-evil-text-change-back-to-indentation
    (kbd "C-k") #'dm-evil-text-change-to-end-of-line)

  nil)

(provide 'dm-evil-text)
;;; dm-evil-text.el ends here
